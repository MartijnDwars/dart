module Statement

imports
  
  Common
  Expression
  Variable
  Arguments

context-free syntax
  
  Statement.Block = <{
    <Statement+>
  }>
  
  Statement.Nop = <;>
  
  Statement.Expression = <<Expression>;>
  
  Statement.If = <if (<Expression>)
    <Statement>>
  
  Statement.IfElse = <if (<Expression>)
    <Statement>
  else
    <Statement>>
  
  Statement.For = <for (<Expression?>; <Expression?>; <{Expression ", "}*>)
    <Statement>>
  
  Statement.ForIn = <for (<ID> in <Expression>)
    <Statement>>
  
  Statement.While = <while (<Expression>)
    <Statement>>
  
  Statement.Do = <do <Statement> while (<Expression>);>
  
  Statement.Switch = <switch (<Expression>) {
    <SwitchCase*>
    <DefaultCase?>
  }>
  
  SwitchCase.SwitchCase = <case <Expression>: <Statement*>>
  
  DefaultCase.DefaultCase = <default: <Statement*>>
  
  Statement.Rethrow = <rethrow;>
  
  Statement.Return = <return <Expression?>;>
  
  Statement.Break = <break <ID?>;>
  
  Statement.Continue = <continue <ID?>;>
  
  Statement.Yield = <yield <Expression>;>
  
  Statement.Assert = <assert (<Expression>);> // TODO: This should be 'ConditionalExpression', what is the influence of our change?
  
  Statement.Call = <<ID>(<Arguments>);>

template options

	tokenize : ";("
  keyword -/- [a-zA-Z]
