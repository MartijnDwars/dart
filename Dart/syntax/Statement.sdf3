module Statement

imports
  
  Arguments
  Common
  Expression
  Type
  Variable
  statement/For
  statement/Try
  
context-free syntax
  
  Statement.Block = <{
    <Statement*>
  }>
  
  Statement.Nop = <;>
  
  Statement.Expression = <<Expression>;>
  
  // TODO: Where is VariableDeclaration used? Can we make the AST more pretty? (Double VarDecl/VariableDeclaration constructor now)
  Statement.VarDecl = <<VariableDeclaration>;>
  
  Statement.If = <if (<Expression>)
    <Statement>>
  
  Statement.IfElse = <if (<Expression>)
    <Statement>
  else
    <Statement>>
  
  Statement.While = <while (<Expression>)
    <Statement>>
  
  Statement.Do = <do <Statement> while (<Expression>);>
  
  Statement.Switch = <switch (<Expression>) {
    <SwitchCase*>
    <DefaultCase?>
  }>
  
  SwitchCase.SwitchCase = <case <Expression>: <Statement*>>
  
  DefaultCase.DefaultCase = <default: <Statement*>>
  
  Statement.Return = <return <Expression?>;>
  
  Statement.Break = <break <ID?>;>
  
  Statement.Continue = <continue <ID?>;>
  
  Statement.Yield = <yield <Expression>;>
  
  Statement.Assert = <assert (<Expression>);> // TODO: This should be 'ConditionalExpression', what is the influence of our change?

template options

	tokenize : ";("
  keyword -/- [a-zA-Z]
