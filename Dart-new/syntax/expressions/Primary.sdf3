module expressions/Primary

imports
  
  Common
  Type
  Function
  Arguments
  Parameters
  Expression
  expressions/Literals
  expressions/AssignableExpression
  Statement
  
context-free syntax
  
  Expression = IndexAccess
  
  Expression = FieldAccess
  
  Expression = VarRef
  
  Expression = Function
  
  Expression = Literal
  
  Expression.This = <this>
  
  // TODO: Since <Type> may contain '.', these two are ambiguous
  Expression.NewObject = <new <Type>(<Arguments>)>
  
  Expression.NewObject = <new <Type>.<ID>(<Arguments>)> {prefer}
  
  Expression.Closurization = <new <Type>#>
  
  Expression.Closurization = <new <Type>#.<ID>>
  
  // TODO: Since <Type> may contain '.', these two are ambiguous
  Expression.ConstObject = <const <Type>(<Arguments>)>
  
  Expression.ConstObject = <const <Type>.<ID>(<Arguments>)> {prefer}
  
  Expression.Lambda = <<Parameters> <FunctionModifier?> {
    <Statement*>
  }>
  
  // NOTE: {longest-match} is used because SDF lacks safe precedence, see e.g. http://dx.doi.org/10.1007/978-3-319-02654-1_8. {longest-match} is not supported in core?
  Expression.ShortLambda = <<Parameters> <ASYNC?> =\> <Expression>> {longest-match}
  
  Expression.Call = <<Expression>(<Arguments>)>
  
template options

  tokenize : "?.["
  