module class/Declaration

imports
  
  class/Initializer
  lexical/Operator
  Common
  Parameters
  Type
  Function
  Arguments
  Expression
  
context-free syntax // Class member declarations
  
  // {prefer} to disambiguate with potentially overlapping ClassMember.FunctionDeclaration
  ClassMember.ConstructorDeclaration = <<CONST?> <Qualified><Parameters> <RedirectionOrInitializers?>;> {prefer}
  
  ClassMember.ExternalConstructorDeclaration = <external <CONST?> <Qualified><Parameters>;>
  
  ClassMember.OperatorDeclaration = <<EXTERNAL?> <ReturnType> operator <Operator><Parameters>;>
  
  ClassMember.StaticFinalDeclaration = <static <FinalOrConst> <DynType> <{StaticFinalDeclaration ", "}+>;>
  
  ClassMember.FinalFieldDeclaration = <final <DynType> <{InitializedIdentifier ", "}+>;>
  
  ClassMember.FieldDeclaration = <<STATIC?> <VarOrType> <{InitializedIdentifier ", "}+>;>
  
  ClassMember.GetterDeclaration = <<FunctionModifier?> <ReturnType> get <ID>;>
  
  ClassMember.SetterDeclaration = <<FunctionModifier?> <ReturnType> set <ID><Parameters>;>
  
  ClassMember.FunctionDeclaration = <<FunctionModifier?> <FunctionSignature>;>
  
context-free syntax // Helpers

  FunctionModifier.FunctionModifier = <<EXTERNAL> <STATIC?>>

  RedirectionOrInitializers.Redirection = <: this(<Arguments>)>
  RedirectionOrInitializers.RedirectionMethod = <: this.<ID>(<Arguments>)>
  RedirectionOrInitializers.Initializer = <: <{SuperCallOrFieldInitializer ", "}+>>
  
  StaticFinalDeclaration = <<ID> = <Expression>>
  
  InitializedIdentifier = <<ID> = <Expression>>
  InitializedIdentifier = <<ID>>
  
  FinalOrConst = "final"
  FinalOrConst = "const"
  
  VarOrType = "var"
  VarOrType = Type
  
template options

  tokenize : ".("
  